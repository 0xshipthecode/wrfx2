(ns nlist
  (require file nlscanner nlparser plist lists
           (joxa-core :as jxc)
           (erlang :as erl :joxify)
           (io_lib :as iol)
           (string :as str :joxify)))


(defn+ parse (fname nlname)
  (case (nlparser/parse (nlscanner/scan fname))
    ({:ok nls} {:nls nlname nls})
    ({:error {lineno module text}} {:error (lists/flatten (iol/format "~p on line ~p" [text lineno]))})))


(defn+ render-value (v)
  "Render the value <v> so that it is readable for a FORTRAN parser."
  (case v
    (:false ".false.")
    (:true  " .true.")
    (lst (when (erl/is-list v)) (iol/format "'~s'" [lst]))
    (other (iol/format "~p" [other]))))


(defn+ render-entry (entry)
  "Render an <entry> (= { key values}) as 'key = values' row."
  (jxc/let ({key vals} entry)
    [key (lists/duplicate (erl/- 36 (erl/length key)) 32) "=\t" (str/join (lists/map render-value/1 vals) ",\t") " \n"]))


(defn+ render-nlist (nl)
  "Render a namelist <nl> as required by FORTRAN parsers."
  (jxc/let ({name entries} nl)
    (lists/flatten ["&" name "\n" (lists/map render-entry/1 entries) "/\n\n"])))


(defn+ render-namelists (nls)
  "Render all namelists in the namelist structure <nls>."
  (jxc/let ({:nls _nlname nlists} nls)
    (lists/map render-nlist/1 nlists)))


(defn+ get-nl (nlname nls)
  "Retrieve the namelist <nlname> from the namelists <nls>."
  (jxc/let ({:nls name nlists} nls)
    (plist/get nlname nlists)))


(defn+ set-nl (nlname new-nl nls)
  "Add/update a namelist <new-nl> with name <name> to <nls>."
  (jxc/let ({:nls name nlists} nls)
    {:nls name (plist/set nlname new-nl nlists)}))


(defn+ get-entry (nlname key nls)
  "Retrieve the entry <key> from the namelist <nlname> from the namelists <nls>."
  (plist/get key (get-nl nlname nls)))


(defn+ set-entry (nlname key vals nls)
  "Set the entry <key> to values <vals> inside the namelist <nlname> of the namelists <nls>."
  (set-nl nlname (plist/set key vals (get-nl nlname nls)) nls))


