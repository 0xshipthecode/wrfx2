(ns ext-proc
    (require file lists filename io os
             (string :as str)
             (io_lib :as iol)
             (time-arith :as ta)
             (erlang :as erl :joxify))
    (use utils joxa-core ext-utils))


;; construct an immediate run script
(defn+ make-immed-exec-script (tid cmd output-spec)
  (iol/format
   (lists/flatten
    [ "#!/usr/bin/env bash\n"
      "CMD=~p\n"
      "TID=~p\n"
      "PIDFILE=\"$TID.pid\"\n"
      "DONEFILE=\"$TID.exitcode\"\n"
      (lists/map (fnm1 {_fd tgt} (iol/format "touch ~s~n" [tgt])) output-spec)
      cmd (lists/map (fnm1 {fd tgt} (iol/format " ~p>> ~s" [fd tgt])) output-spec) " &\n"
      "PID=$!\n"
      "echo $PID > $PIDFILE\n"
      "echo `date +%s` >> $PIDFILE\n"
      "wait $PID\n"
      "echo $? > $DONEFILE\n"]
      "echo `date +%s` >> $DONEFILE\n")
        [cmd tid]))


;; execute a process denoted tid with a given timeout in a given directory
;; the execute function either returns {:success exit-code} if process need not
;; be executed or {:running pid} if the process was executed and running
;; in this case, the process termination is transmitted to the caller as
;; {:ext-proc-complete task-pid {result message}} where result is either :success or :failure
(defn+ run (tid command output-spec in-dir exec-timeout log-f)
  ;; check if the command has already been run (exitcode file exists)
  (let ([pid-f exit-code-f] (make-proc-names in-dir tid [".pid" ".exitcode"]))
    (case (read-exitcode-file exit-code-f)
      ({exit-code exit-time}
        ;; just return the exit code we already obtained
        (log-f :info "~s: exit code ~p found, process exited on ~w returning immediately" [tid exit-code exit-time])
        {:success exit-code})
     (:invalid
        (log-f :info "~s: no exit code found, will check pid file ~s" [tid pid-f])
        (case (read-pid-file pid-f)
          ({os-pid start-time}
            (let* (remaining-exec-timeout (- exec-timeout (erl/* (ta/seconds-elapsed-from start-time) 1000)))
              (log-f :info "~s: found pid file with pid ~p, written at ~p, waiting for another ~p ms" [tid os-pid start-time remaining-exec-timeout])
              (begin-monitoring in-dir tid 5000 remaining-exec-timeout log-f)))
          (:invalid
            (log-f :info "~s: no pid file found, process ~p must be started" [tid command])
            (let* (fqn (make-proc-file-name in-dir tid ".sh"))
              (write-run-script fqn (make-immed-exec-script tid command output-spec))
              (erl/open-port {:spawn fqn} [{:cd in-dir}])
              (begin-monitoring in-dir tid 5000 exec-timeout log-f))))))))

